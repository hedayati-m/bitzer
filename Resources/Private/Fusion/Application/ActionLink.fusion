prototype(Sitegeist.Bitzer:ActionLink) < prototype(Neos.Fusion:Component) {
    actionName = ''
    actionArguments = Neos.Fusion:DataStructure
    icon = ''
    contentMode = 'icon'

    renderer = Neos.Fusion:Tag {
        tagName = 'a'
        attributes {
            href = Neos.Fusion:UriBuilder {
                package = 'Sitegeist.Bitzer'
                subpackage = 'Application'
                controller = 'Bitzer'
                action = ${props.actionName}
                arguments = ${props.actionArguments}
            }
            class="neos-button neos-button-primary"
            data-neos-toggle="tooltip"
            data-original-title = ${Translation.translate('actions.' + props.actionName + '.label', null, [], 'Module.Bitzer', 'Sitegeist.Bitzer')}
        }
        content = Neos.Fusion:Case {
            icon {
                condition = ${props.contentMode == 'icon'}
                renderer = ${'<i class="fas fa-' + props.icon + ' icon-white"></i>'}
            }
            label {
                condition = ${props.contentMode == 'label'}
                renderer = ${Translation.translate('actions.' + props.actionName + '.label', null, [], 'Module.Bitzer', 'Sitegeist.Bitzer')}
            }
        }
    }
}
