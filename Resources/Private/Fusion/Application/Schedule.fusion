prototype(Sitegeist.Bitzer:Application.Schedule) < prototype(Neos.Fusion:Component) {
    tasks = ${[]}

    renderer = afx`
        <Sitegeist.Bitzer:FlashMessages />
        <table class="neos-table">
            <thead>
                <tr>
                    <th>{${Translation.translate('task.scheduledDate.label', null, [], 'Module.Bitzer', 'Sitegeist.Bitzer')}}</th>
                    <th>{${Translation.translate('task.actionStatus.label', null, [], 'Module.Bitzer', 'Sitegeist.Bitzer')}}</th>
                    <th>{${Translation.translate('task.description.label', null, [], 'Module.Bitzer', 'Sitegeist.Bitzer')}}</th>
                    <th>{${Translation.translate('task.agent.label', null, [], 'Module.Bitzer', 'Sitegeist.Bitzer')}}</th>
                    <th>{${Translation.translate('actions.label', null, [], 'Module.Bitzer', 'Sitegeist.Bitzer')}}</th>
                </tr>
            </thead>
            <tbody>
                <Sitegeist.Bitzer:ScheduleTable tasks={props.tasks} @if.hasTasks={props.tasks} />
            </tbody>
        </table>
        <Sitegeist.Bitzer:ToggleScript />
    `
}

prototype(Sitegeist.Bitzer:ScheduleTable) < prototype(Neos.Fusion:Component) {
    tasks = ${[]}
    groupIdentifier = ''

    renderer = afx`
        <Neos.Fusion:Collection collection={props.tasks}>
            <tr>
                <td>{Date.format(item.scheduledTime, 'Y-m-d H:i')}</td>
                <td>{${Translation.translate('actionStatusType.' + String.toString(item.actionStatus) + '.label', null, [], 'Module.Bitzer', 'Sitegeist.Bitzer')}}</td>
                <td>{item.description}</td>
                <td>{item.agent}</td>
                <td class="neos-action">
                    <div class="neos-pull-right">
                        <Sitegeist.Bitzer:ActionLink actionName="editTask" actionArguments={{taskIdentifier: String.toString(item.identifier)}} icon="edit" />
                        <Sitegeist.Bitzer:CommandForm commandName="completeTask" commandArguments={{taskIdentifier: String.toString(item.identifier)}} icon="check" />
                        <Sitegeist.Bitzer:CommandForm commandName="cancelTask" commandArguments={{taskIdentifier: String.toString(item.identifier)}} icon="times" />
                    </div>
                </td>
            </tr>
        </Neos.Fusion:Collection>`
}
