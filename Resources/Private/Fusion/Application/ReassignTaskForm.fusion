prototype(Sitegeist.Bitzer:ReassignTaskForm) < prototype(Neos.Fusion:Component) {
    task = null
    agents = ${[]}
    constraintCheckResult = null

    @context.reassignUri = Neos.Fusion:UriBuilder {
        package = 'Sitegeist.Bitzer'
        subpackage = 'Application'
        controller = 'Bitzer'
        action = 'reassignTask'
    }

    renderer = afx`
        <form action={reassignUri} method="POST">
            <fieldset class="neos-span5">
                <legend>{${Translation.translate('reassignTask.label', null, [], 'Module.Bitzer', 'Sitegeist.Bitzer')}}</legend>
                <input type="hidden" name="__csrfToken" value={${Security.csrfToken()}} />
                <input type="hidden" name="moduleArguments[taskIdentifier]" value={${String.toString(props.task.identifier)}} />
                <div class={${'neos-control-group' + (constraintCheckResult.hasFailedAtPath('agent') ? ' neos-error' : '')}}>
                    <label class="neos-control-label" for="title">{${Translation.translate('task.agent.label', null, [], 'Module.Bitzer', 'Sitegeist.Bitzer')}}</label>
                    <div class="neos-controls neos-controls-row">
                        <select id="agent" name="moduleArguments[agent]">
                            <Neos.Fusion:Collection collection={props.agents} itemName="agentIdentifier">
                                <option value={agentIdentifier} @if.isNotAssigned={agentIdentifier != props.task.agent}>{agentIdentifier}</option>
                                <option value={agentIdentifier} selected="selected" @if.isAssigned={agentIdentifier == props.task.agent}>{agentIdentifier}</option>
                            </Neos.Fusion:Collection>
                        </select>
                    </div>
                </div>
                <input class="neos-button neos-button-primary" type="submit" name="" value={${Translation.translate('reassignTask.label', null, [], 'Module.Bitzer', 'Sitegeist.Bitzer')}} />
            </fieldset>
        </form>
    `
}
